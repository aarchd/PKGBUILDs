# Maintainer: Deepak Kumar <notwho53@gmail.com>
pkgname=libhybris
pkgver=0.0.5.53
pkgrel=1
arch=('aarch64')
url="https://github.com/libhybris/libhybris"
license=('LGPL')
depends=('glibc')
makedepends=(
  'android-headers'
  'wayland'
  'wayland-protocols'
  'libx11'
  'libxcb'
  'libxext'
  'binutils'
  'patchelf'
  'python'
  'glibc'
  'pkgconf'
  'mesa'
)
source=("git+https://github.com/droidian/libhybris.git"
  "0002-Fix-gralloc-wrapper-include-file-installation.patch")
sha256sums=('SKIP'
  '75de53d880ad70cc37d82772f9331884a310ac952e22262dca70b8b34fb8a1bb')

build() {
  patch -p1 < "$srcdir/0002-Fix-gralloc-wrapper-include-file-installation.patch" -d "$srcdir/$pkgbase"
  cd "$srcdir/$pkgbase/hybris"

  autoreconf -fi
  ./configure --enable-wayland --with-android-headers=/usr/include/android --enable-property-cache --enable-experimental --enable-glvnd --enable-clicd --enable-arch=arm64 --enable-mali-quirks --enable-adreno-quirks --prefix=/usr 
  make
}

package_libhybris() {
  pkgdesc="Allows use of bionic-based HW adaptations"

  cd "$srcdir/$pkgbase/hybris"
  DESTDIR="$pkgdir" make install
  rm -rf $pkgdir/usr/include/{CL,EGL,GLES{,2,3},KHR}
  for man in getprop setprop; do
    gzip -n -c "../debian/$man.1" > "$man.1.gz"
    install -Dm644 "$man.1.gz" "$pkgdir/usr/share/man/man1/$man.1.gz"
  done
}
