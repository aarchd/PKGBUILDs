# Maintainer: Deepak Meena <notwho53@gmail.com>
pkgname=ofono
pkgver=1.29
pkgrel=1
pkgdesc='Infrastructure for building mobile telephony (GSM/UMTS) applications'
url='https://01.org/ofono'
arch=(aarch64)
license=('GPL2')
depends=('bluez' 'dbus-glib' 'glib2' 'udev' 'mobile-broadband-provider-info' 'systemd' 'python')
makedepends=('bluez' 'dbus-glib' 'glib2-devel' 'udev' 'mobile-broadband-provider-info' 'systemd' 'libglibutil-dev' 'libgrilio-dev'
	'libwspcodec-dev')
source=('git+https://github.com/sailfishos/ofono.git'
	'ofono.service')
sha256sums=('SKIP'
	'58aa96394b43fcbf147e0bdb6644d5089ece95e91aefb906a16ceec01f8e657c')
install=ofono.install

build() {
	cd $pkgname/$pkgname

	export CFLAGS="$CFLAGS -Wall \
		-Wno-unused-parameter \
		-Wno-missing-field-initializers \
		-Wdeclaration-after-statement \
		-Wmissing-declarations \
		-Wcast-align \
		-DG_DISABLE_DEPRECATED"

	./bootstrap
	autoreconf -fi
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-test \
		--disable-static \
		--enable-sailfish-bt \
		--enable-sailfish-provision \
		--enable-sailfish-pushforwarder \
		--disable-rilmodem \
		--disable-add-remove-context \
		--disable-isimodem \
		--disable-qmimodem \
		--disable-upower \
		--with-systemdunitdir=/usr/lib/systemd/system
	make
}

package() {
	cd $pkgname/$pkgname
	make DESTDIR="$pkgdir" install

	install -d "$pkgdir/usr/share/ofono/scripts"
	mv "$pkgdir/usr/lib/ofono/test/"* "$pkgdir/usr/share/ofono/scripts/"
	rm -r "$pkgdir/usr/lib/ofono/test"

	install -Dm644 "$srcdir/ofono.service" "$pkgdir/usr/lib/systemd/system/ofono.service"
}
