# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable x86 extensions (mmx, sse2, ssse3)

# AArchD: Deepak Meena <who53@disroot.org>
# - enable asimd
# - drop checks

pkgname=pixman
pkgver=0.46.2
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=('aarch64')
url="https://cgit.freedesktop.org/pixman/"
license=('MIT')
depends=('glibc')
makedepends=('meson' 'libpng')
provides=('libpixman-1.so')
source=(https://www.cairographics.org/releases/${pkgname}-${pkgver}.tar.xz)
sha512sums=('9419e5a23f4b709e075c5dd33c46bb375d2407b6e90623fe3410a03171f4c5e0a511e8b4414557a39b68d46a4c1ac64f2ef0c9c6eab0b42a2be75d5d52a367fd')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  arch-meson --buildtype=release $pkgname-$pkgver build \
    -D loongson-mmi=disabled \
    -D vmx=disabled \
    -D arm-simd=disabled \
    -D neon=disabled \
    -D a64-neon=enabled \
    -D rvv=disabled \
    -D mmx=disabled \
    -D sse2=disabled \
    -D ssse3=disabled \
    -D mips-dspr2=disabled \
    -D gtk=disabled
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}
