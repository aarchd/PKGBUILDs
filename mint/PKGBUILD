# Maintainer: Deepak Meena <notwho53@gmail.com>
pkgname=mint
pkgver=0.5
pkgrel=2
pkgdesc="minui-based splash and info display tool for Linux and Android devices"
arch=('aarch64')
url="https://github.com/aarchd/mint"
depends=('android-minui' 'cairo' 'freetype2')
makedepends=('meson' 'android-minui' 'cairo')
source=("git+$url.git"
    "mint.initcpio_install"
    "mint.initcpio_hook")
sha256sums=('SKIP'
    '106a0e22e2dc2ead12abfee6d428411defac4f9ea945c30f9ff8ed1a860b201f'
    '023514c26e860dfe5b5213251bf03a03ef0eb87a03fb28edf6bbc2d58da50324')

build() {
	arch-meson "$srcdir/$pkgname" build
	ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 "$srcdir/mint.initcpio_install" "$pkgdir/usr/lib/initcpio/install/mint"
    install -Dm644 "$srcdir/mint.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/mint"
}
