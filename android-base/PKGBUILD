# Maintainer: Deepak Meena <who53@disroot.org>

pkgname=android-base
pkgver=2
pkgrel=0
pkgdesc="Android base mountpoints, user and groups"
arch=('any')
license=('GPL-2.0-or-later')
url="https://github.com/droidian/android-base"
source=("git+https://github.com/aarchd/android-base.git"
	'android-base.hook'
	'update-hook.sh')
sha256sums=('SKIP'
		'ffa4206c1d1168157a182630464c6b581e309ea3c0e46fa45f574c192c08f769'
		'db6ca9e68735ee614e25e556ecf07c8d38eab6d5cf4254776f72ffb303176171')

package() {
	# mountpoints
	for symlink in data system vendor cache persist product metadata apex odm vendor_dlkm; do
	    ln -s "/android/$symlink" "${pkgdir}/$symlink"
	done

	cd ${srcdir}/${pkgname}
	# user UID, GID setup
	install -Dm644 "login.defs" "${pkgdir}/usr/share/android-base/login.defs"
	install -Dm644 "useradd" "${pkgdir}/usr/share/android-base/useradd"
	
	# re-update after shadow or android-base package gets updated
	install -Dm644 "${srcdir}/android-base.hook" "${pkgdir}/usr/share/libalpm/hooks/android-base.hook"
	install -Dm755 "${srcdir}/update-hook.sh" "${pkgdir}/usr/share/android-base/update-hook.sh"

	# android user and groups
	install -Dm644 "sysusers.d/android.conf" "${pkgdir}/usr/lib/sysusers.d/android.conf"
}
