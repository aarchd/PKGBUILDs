# Maintainer: Deepak Meena <notwho53@gmail.com>
pkgbase=pulseaudio-modules-droid-modern
pkgname=('pulseaudio-modules-droid-modern' 'pulseaudio-modules-droid')
pkgver=14.2.103
pkgrel=1
arch=('aarch64')
url="https://github.com/mer-hybris/pulseaudio-modules-droid-modern"
license=('custom')
makedepends=('dbus' 'android-headers' 'pulseaudio' 'pulsecore' 'expat' 'libevdev' 'udev' 'meson' 'libhybris')
source=("git+https://github.com/droidian/pulseaudio-modules-droid-modern.git")
sha256sums=('SKIP')

prepare() {
    cd "$srcdir/$pkgname"
    echo "$pkgver" > .tarball-version
}

build() {
  PA_MODULE_DIR=$(readlink -f /usr/lib/pulse-*/modules)
  arch-meson $pkgname build -Dmodlibexecdir=${PA_MODULE_DIR}
  meson compile -C build
}

package_pulseaudio-modules-droid-modern() {
  pkgdesc="PulseAudio Droid HAL module"
  depends=('pulseaudio')
  provides=('pulseaudio-modules-droid-apispecific')
  conflicts=('pulseaudio-modules-droid-apispecific')
  DESTDIR="$pkgdir" meson install -C build
}

package_pulseaudio-modules-droid() {
  pkgdesc="PulseAudio Droid HAL module - transitional package"
  depends=('pulseaudio-modules-droid-modern')
}
