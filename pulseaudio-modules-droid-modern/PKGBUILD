# Maintainer: Deepak Meena <who53@disroot.org>

pkgbase=pulseaudio-modules-droid-modern
pkgname=('pulseaudio-modules-droid-modern' 'pulseaudio-modules-droid')
pkgver=14.2.103
pkgrel=1
pkgdesc="Pulseaudio modules to interact with the Android HAL."
arch=('aarch64')
url="https://github.com/mer-hybris/pulseaudio-modules-droid-modern"
license=('LGPL-2.1-only')
depends=('libpulse' 'expat' 'libevdev' 'udev' 'libhybris' 'pulseaudio' 'pulsecore')
makedepends=('dbus' 'android-headers' 'meson')
source=("git+https://github.com/droidian/pulseaudio-modules-droid-modern.git")
sha256sums=('SKIP')

prepare() {
    cd "$srcdir/$pkgname"
    echo "$pkgver" > .tarball-version
}

build() {
  PA_MODULE_DIR=$(readlink -f /usr/lib/pulse-*/modules)
  arch-meson --buildtype=release $pkgname build -Dmodlibexecdir=${PA_MODULE_DIR}
  meson compile -C build
}

package_pulseaudio-modules-droid-modern() {
  pkgdesc="PulseAudio Droid HAL module"
  depends=('pulseaudio')
  provides=('pulseaudio-modules-droid-apispecific')
  conflicts=('pulseaudio-modules-droid-apispecific')
  DESTDIR="$pkgdir" meson install -C build
}

package_pulseaudio-modules-droid() {
  pkgdesc="PulseAudio Droid HAL module - transitional package"
  depends=('pulseaudio-modules-droid-modern')
}
