# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# AArchD
# Maintainer: Deepak Meena <who53@disroot.org>

pkgbase=pulseaudio
pkgname=(pulseaudio libpulse pulsecore pulseaudio-{zeroconf,lirc,jack,bluetooth,equalizer,rtp})
pkgdesc="A featureful, general-purpose sound server"
pkgver=14.2
pkgrel=2
arch=(aarch64)
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=(LGPL-2.1-or-later)
makedepends=('libasyncns' 'libcap' 'attr' 'libxtst' 'libsm' 'libsndfile' 'rtkit' 'libsoxr'
    'speexdsp' 'tdb' 'systemd' 'dbus' 'avahi' 'bluez' 'bluez-libs' 'jack2' 'sbc'
    'lirc' 'openssl' 'fftw' 'orc' 'gtk3' 'webrtc-audio-processing-1' 'check' 'git' 'meson'
    'xmltoman' 'doxygen')
source=("git+https://github.com/droidian/pulseaudio.git"
    "build-sys-meson-Add-missing-include-dir.patch"
    "0001-meson-add-pulsecore.pc.in.patch")
sha256sums=('SKIP'
    'afd7d4b1991636d7e72ee80df609a609071309d331adaa933af9582010411426'
    '5b2a3683b911108e43401038eaf9789b45ddde9f027f04776c492c20efb8bdae')

prepare() {
    cd pulseaudio
    patch -p1 < ../build-sys-meson-Add-missing-include-dir.patch
    patch -p1 < ../0001-meson-add-pulsecore.pc.in.patch
    echo "$pkgver-$pkgrel" > .tarball-version
}

build() {
    arch-meson --buildtype=release pulseaudio build \
        -D pulsedsp-location='/usr/\$LIB/pulseaudio' \
        -D stream-restore-clear-old-devices=true \
        -D udevrulesdir=/usr/lib/udev/rules.d
    meson compile -C build
}

_pick() {
    local p="$1" f d; shift
    for f; do
        d="$srcdir/$p/${f#$pkgdir/}"
        mkdir -p "$(dirname "$d")"
        mv "$f" "$d"
        rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
    done
}

package_pulseaudio() {
    depends=(libpulse rtkit libltdl speexdsp tdb orc libsoxr
        webrtc-audio-processing libxtst)
    optdepends=('pulseaudio-alsa: ALSA configuration (recommended)'
        'pulseaudio-zeroconf: Zeroconf support'
        'pulseaudio-lirc: IR (lirc) support'
        'pulseaudio-jack: Jack support'
        'pulseaudio-bluetooth: Bluetooth support'
        'pulseaudio-equalizer: Graphical equalizer'
        'pulseaudio-rtp: RTP and RAOP support')
    backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
    install=pulseaudio.install

    local pulsever=$(cd pulseaudio; ./git-version-gen .tarball-version)
    while [[ $pulsever = *.*.* ]]; do
        pulsever=${pulsever%.*}
    done
    pulsever=${pulsever%%-*}

    DESTDIR="$pkgdir" meson install -C build

    cd "$pkgdir"

    # Superseded by socket activation
    sed -e '/autospawn/iautospawn = no' \
        -i etc/pulse/client.conf

    # Disable cork-request module, can result in e.g. media players unpausing
    # when there's a Skype call incoming
    sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
        -i usr/bin/start-pulseaudio-x11

    # Required by qpaeq
    sed -e '/Load several protocols/aload-module module-dbus-protocol' \
        -i etc/pulse/default.pa

    rm -r etc/dbus-1

    # Split packages
    _pick libpulse etc/pulse/client.conf
    _pick libpulse usr/bin/pa{cat,ctl,dsp,mon,play,rec,record}
    _pick libpulse usr/lib/libpulse{,-simple,-mainloop-glib}.so*
    _pick libpulse usr/lib/{cmake,pkgconfig}
    _pick libpulse usr/lib/pulseaudio/libpulse{dsp,common-*}.so
    _pick libpulse usr/include
    _pick libpulse usr/share/man/man1/pa{cat,ctl,dsp,mon,play,rec,record}.1
    _pick libpulse usr/share/man/man5/pulse-client.conf.5
    _pick libpulse usr/share/vala

    local moddir=usr/lib/pulse-$pulsever/modules

    _pick zeroconf $moddir/libavahi-wrap.so
    _pick zeroconf $moddir/module-zeroconf-{publish,discover}.so
    _pick zeroconf $moddir/module-raop-discover.so

    _pick lirc $moddir/module-lirc.so

    _pick jack $moddir/module-jack-{sink,source}.so
    _pick jack $moddir/module-jackdbus-detect.so

    _pick bluetooth $moddir/libbluez5-util.so
    _pick bluetooth $moddir/module-bluetooth-{discover,policy}.so
    _pick bluetooth $moddir/module-bluez5-{discover,device}.so

    _pick equalizer $moddir/module-equalizer-sink.so
    _pick equalizer usr/bin/qpaeq

    _pick rtp $moddir/lib{rtp,raop}.so
    _pick rtp $moddir/module-rtp-{send,recv}.so
    _pick rtp $moddir/module-raop-sink.so
}

package_libpulse() {
    pkgdesc="$pkgdesc (client library)"
    depends=(dbus libasyncns libcap libxcb libsm libsndfile systemd)
    optdepends=('glib2: mainloop integration')
    provides=(libpulse{,-simple,-mainloop-glib}.so)
    backup=(etc/pulse/client.conf)

    mv libpulse/* "$pkgdir"
}

package_pulseaudio-zeroconf() {
    pkgdesc="Zeroconf support for PulseAudio"
    depends=("pulseaudio" avahi openssl)

    mv zeroconf/* "$pkgdir"
}

package_pulseaudio-lirc() {
    pkgdesc="IR (lirc) support for PulseAudio"
    depends=("pulseaudio" lirc)

    mv lirc/* "$pkgdir"
}

package_pulseaudio-jack() {
    pkgdesc="Jack support for PulseAudio"
    depends=("pulseaudio" jack)

    mv jack/* "$pkgdir"
}

package_pulseaudio-bluetooth() {
    pkgdesc="Bluetooth support for PulseAudio"
    depends=("pulseaudio" bluez bluez-libs sbc)

    mv bluetooth/* "$pkgdir"
}

package_pulseaudio-equalizer() {
    pkgdesc="Graphical equalizer for PulseAudio"
    depends=("pulseaudio" python-{pyqt5,dbus} fftw)

    mv equalizer/* "$pkgdir"
}

package_pulseaudio-rtp() {
    pkgdesc="RTP and RAOP support for PulseAudio"
    depends=("pulseaudio")

    mv rtp/* "$pkgdir"
}

package_pulsecore() {
    pkgdesc="PulseAudio client development headers and libraries (out of tree modules support)"
    depends=("pulseaudio" "libpulse")

    #have to do it manually build system doesnt install it at path
    mkdir -p "$pkgdir/usr/include/pulsecore"
    cp -pR pulseaudio/src/pulsecore/* "$pkgdir/usr/include/pulsecore"
    find "$pkgdir/usr/include/pulsecore" -type f ! -name '*.h' -delete
    rm "$pkgdir/usr/include/pulsecore/Makefile"
}
