# Maintainer: Deepak Meena <notwho53@gmail.com>
pkgname=sensorfw
pkgver=0.15.1
pkgrel=1
pkgdesc="Sensor framework daemon"
arch=('aarch64')
url="https://github.com/sailfishos/sensorfw"
license=('GPL-2.1')
makedepends=('qt6-base' 'udev' 'systemd' 'libhardware-dev' 'android-headers' 
  'libhybris-dev' 'glib2-devel' 'libglibutil-dev' 'libgbinder-dev')
source=("git+$url.git"
    "0001-drop-doc-test-example-qt-api.patch"
    "sensorfwd.service"
    "sensorfwd-hybris-dropin.conf")
sha256sums=('SKIP'
    '0d2a4c54f1c49e1d62571b4562a1ec087d335be0603ba5178abd582e7c30631a'
    'b9223498ed0a9c15cd317d5375ee8f211f5508b4c684576fa1937f3b47391361'
    '1282f58a06ef5b4265f051097c0fba95aa6e69d9482d597fd560a2d680196271')

_projname=sensorfw
_builddir=build

prepare() {
    cd "$srcdir/$pkgname"
    patch -p1 < "$srcdir/0001-drop-doc-test-example-qt-api.patch"
    mkdir -p "$_builddir"
}

build() {
    cd "$srcdir/$pkgname/$_builddir"

    export CFLAGS="-Wall -Wno-psabi"
    [[ "$CFLAGS" != *-O* ]] && CFLAGS+=" -O2"

    qmake6 CONFIG+="configs autohybris binder systemdunit" \
        QMAKE_CXXFLAGS="$CFLAGS" ../${_projname}.pro

    make -j$(nproc)

    sed "s|@LIB@|lib|g" "$srcdir/$pkgname/sensord-qt6.pc.in" \
        > "$srcdir/$pkgname/sensord-qt6.pc"
}

package() {
    cd "$srcdir/$pkgname/$_builddir"

    make INSTALL_ROOT="$pkgdir" install

    mv "$pkgdir/lib/"* "$pkgdir/usr/lib/"
    rm -rf "$pkgdir/lib/"
    mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
    rm -rf "$pkgdir/usr/sbin/"

    install -Dm644 "$srcdir/$pkgname/sensord-qt6.pc" \
        "$pkgdir/usr/lib/pkgconfig/sensord-qt6.pc"

    install -Dm644 "$srcdir/sensorfwd.service" \
        "$pkgdir/usr/lib/systemd/system/sensorfwd.service"

    install -Dm644 "$srcdir/sensorfwd-hybris-dropin.conf" \
        "$pkgdir/usr/lib/systemd/system/sensorfwd.service.d/sensorfwd-hybris-dropin.conf"

    cp -a "$srcdir/$pkgname/config/"* "$pkgdir/etc/sensorfw/"
}
