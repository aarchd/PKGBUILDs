# Maintainer: Deepak Meena <who53@disroot.org>

pkgbase=ofono-ril-binder-plugin
pkgname=(
	'ofono-ril-binder-plugin'
	'libgrilio-binder'
)
pkgver=1.2.8
pkgrel=1
pkgdesc="ofono plugin which implements binder transport for RIL"
arch=('aarch64')
url="https://github.com/mer-hybris/ofono-ril-binder-plugin"
license=('BSD-3-Clause')
makedepends=('ofono')
source=("git+${url}.git")
sha256sums=('SKIP')

build() {
	cd $srcdir/$pkgbase
	make KEEP_SYMBOLS=1 release pkgconfig
	make DESTDIR="$srcdir/build" install
	make DESTDIR="$srcdir/build" install-dev
}

package_ofono-ril-binder-plugin() {
	depends=('libgrilio-binder')

	install -Dm644 $srcdir/build/usr/lib/ofono/plugins/rilbinderplugin.so \
		"$pkgdir/usr/lib/ofono/plugins/rilbinderplugin.so"
	install -Dm644 $srcdir/$pkgbase/LICENSE \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_libgrilio-binder() {
	depends=('libgbinder-radio' 'libgrilio')

	mkdir -p "$pkgdir/usr/lib"
	cp -ar $srcdir/build/usr/lib/libgrilio-binder.so* $pkgdir/usr/lib/
	install -Dm644 $srcdir/build/usr/lib/pkgconfig/libgrilio-binder.pc $pkgdir/usr/lib/pkgconfig/libgrilio-binder.pc
	mkdir -p "$pkgdir/usr/include/grilio-binder"
	install -Dm644 $srcdir/build/usr/include/grilio-binder/*.h $pkgdir/usr/include/grilio-binder
	install -Dm644 $srcdir/$pkgbase/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
