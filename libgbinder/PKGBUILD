# Maintainer: Deepak Meena <who53@disroot.org>

pkgbase=libgbinder
pkgname=(
	'libgbinder'
	'libgbinder-tools'
)
pkgver=1.1.42
pkgrel=1
pkgdesc="GLib-style interface to binder"
arch=('aarch64')
url="https://github.com/mer-hybris/libgbinder"
license=('BSD-3-Clause')
makedepends=('flex' 'bison' 'libglibutil')
source=("git+${url}.git"
	'0001-libgbinder-Add-getTransport-support-to-hidl.patch'
	'0002-libgbinder-Implement-binder-wait.patch')
sha256sums=('SKIP'
	'0805f79cacf6c926981506a4fdaff6f833108408332d8377e40004345ff6c88a'
	'700f6125b62d90668779c3ef8deff11d4ec1105e996ac3c022c0abb92fa0909b')

build() {
	cd $srcdir/$pkgbase
	patch -p1 < $srcdir/0001-libgbinder-Add-getTransport-support-to-hidl.patch
	patch -p1 < $srcdir/0002-libgbinder-Implement-binder-wait.patch
	make KEEP_SYMBOLS=1 release pkgconfig
}

package_libgbinder() {
	depends=('libglibutil')
	cd $srcdir/$pkgbase
	make install DESTDIR="${pkgdir}"
	make install-dev DESTDIR="${pkgdir}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libgbinder-tools() {
	depends=('libgbinder')
	cd $srcdir/$pkgbase/test
	make install DESTDIR="${pkgdir}"
	install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
